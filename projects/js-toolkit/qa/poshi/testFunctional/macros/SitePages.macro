definition {
	macro _addPage {
		SitePages._selectPageType(
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}"
		);

		if (IsElementPresent(locator1 = "IFrame#DIALOG")) {
			SelectFrame(locator1 = "IFrame#DIALOG");
		}

		PortletEntry.inputName(name = "${pageName}");

		Button.click(button = "Save");

		SelectFrame.selectFrameTop();

		if ("${sitePageType}" == "Embedded") {
			Type(
				locator1 = "SitePagesEditPage#PAGE_TYPE_EMBEDDED_URL_FIELD",
				value1 = "${pageTypeURL}"
			);
		}

		if (isSet(inheritChanges)) {
			Page.togglePageTemplateInheritChanges(toggleValue = "${inheritChanges}");
		}

		Button.click(button = "Save");

		WaitForSPARefresh();
	}

	macro _configurePage {
		if ("${sitePageType}" == "Embedded") {
			Page._addPageTypeEmbedded(pageTypeURL = "${pageTypeURL}");
		}
		else if ("${sitePageType}" == "Panel") {
			Page._addPageTypePanel(componentName = "${componentName}");
		}
		else if ("${sitePageType}" == "Widget Page") {
			var key_columnName = "${columnName}";

			Click(locator1 = "SitePagesEditPage#LAYOUT_CARD");
		}
		else if ("${sitePageType}" == "Content Page") {
			Select(
				locator1 = "SitePagesEditPage#LAYOUT_PAGE_TEMPLATE_SELECT",
				value1 = "${layoutPageTemplateName}"
			);
		}
		else if ("${sitePageType}" == "Full Page Application") {
			Page._addPageTypeFullPageApplication(portlet = "${portlet}");
		}
	}

	macro _selectPageType {
		if (contains("${sitePageType}", "{sitePageType}")) {
			var sitePageType = "Widget Page";
		}

		var rowEntry = "${sitePageType}";

		if (!(contains("${pageTemplateName}", "{pageTemplateName}"))) {
			Navigator.gotoNavNested(navNested = "Global Templates");

			var rowEntry = "${pageTemplateName}";

			LexiconEntry.gotoEntry(rowEntry = "${rowEntry}");
		}
		else {
			Card.clickSitePagesCardLink(cardName = "${rowEntry}");
		}
	}

	macro addChildPage {
		MouseOver(
			locator1 = "SitePages#LIST_GROUP_ITEM",
			key_itemName = "${parentPageName}"
		);

		Click(
			locator1 = "SitePages#LIST_GROUP_ITEM_PLUS_ICON",
			key_pageName = "${parentPageName}"
		);

		SitePages._addPage(
			pageName = "${childPageName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}"
		);
	}

	macro addPrivateChildPage {
		SitePages.togglePrivacy(privacy = "Private Pages");

		SitePages.addChildPage(
			childPageName = "${childPageName}",
			pageTemplateName = "${pageTemplateName}",
			parentPageName = "${parentPageName}",
			sitePageType = "${sitePageType}"
		);
	}

	macro addPrivatePage {
		if ("${siteTemplate}" == "true") {
			LexiconEntry.gotoAdd();
		}
		else {
			LexiconEntry.gotoAddMenuItem(menuItem = "Private Page");
		}

		SitePages._addPage(
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			sitePageType = "${sitePageType}"
		);
	}

	macro addPublicChildPage {
		SitePages.togglePrivacy(privacy = "Public Pages");

		SitePages.addChildPage(
			childPageName = "${childPageName}",
			pageTemplateName = "${pageTemplateName}",
			parentPageName = "${parentPageName}",
			sitePageType = "${sitePageType}"
		);
	}

	macro addPublicPage {
		var key_breadcrumbName="Pages";

		if (IsElementPresent(locator1 = "Message#EMPTY_INFO_TAGLIB")) {
			SitePages.gotoAddFirstPage(
				pageType = "Public Page"
			);
		}
		else if (IsElementPresent(locator1 = "SitePages#LIST_GROUP_ITEM", key_itemName = "Public Pages")) {
			Click(locator1 = "SitePages#LIST_GROUP_ITEM", key_itemName = "Public Pages");

			LexiconEntry.gotoAdd();
		}
		else {
			AssertClick(
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1="Pages"
			);

			LexiconEntry.gotoAddMenuItem(menuItem = "Public Page");
		}

		SitePages._addPage(
			inheritChanges = "${inheritChanges}",
			pageName = "${pageName}",
			pageTemplateName = "${pageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			sitePageType = "${sitePageType}"
		);
	}

	macro configureChildPage {
		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "${pageName}",
			parentPagesHierarchy = "${parentPagesHierarchy}"
		);

		SitePages._configurePage(
			columnName = "${columnName}",
			componentName = "${componentName}",
			layoutPageTemplateName = "${layoutPageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			portlet = "${portlet}",
			sitePageType = "${sitePageType}"
		);

		PortletEntry.save();
	}

	macro configurePage {
		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		SitePages.togglePrivacy(privacy = "${privacy}");

		SitePages.gotoPageMenuItem(menuItem = "Configure", pageName = "${pageName}");

		SitePages._configurePage(
			columnName = "${columnName}",
			componentName = "${componentName}",
			layoutPageTemplateName = "${layoutPageTemplateName}",
			pageTypeURL = "${pageTypeURL}",
			portlet = "${portlet}",
			sitePageType = "${sitePageType}"
		);

		PortletEntry.save();
	}

	macro deletePage {
		Click(
			locator1 = "SitePages#LIST_GROUP_ITEM_ELLIPSIS_ICON",
			key_pageName = "${pageName}"
		);

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		if (isSet(failure)) {
			if (IsElementPresent(locator1 = "Message#ERROR_2")) {
				Alert.viewRequestFailedToComplete();

				AssertTextEquals(locator1 = "Message#ERROR_2", value1 = "${failure}");
			}
			else {
				AssertTextEquals(locator1 = "Message#ERROR", value1 = "${failure}");
			}
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro expandPageTree {
		if (!(isSet(depth))) {
			var depth = "1";
		}

		var count = "0";

		while (!("${count}" == "${depth}")) {
			var index = MathUtil.sum("${count}", "1");

			Click(locator1 = "SitePages#LIST_GROUP_ITEM_CARET_ICON", key_depth = "${index}");

			var count = MathUtil.sum("${count}", "1");
		}
	}

	@summary = "Go to the page creation form through the empty-result-message taglib"
	macro gotoAddFirstPage {
		var key_menuItem = "${pageType}";

		Click(
			locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN"
		);

		Click(
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM"
		);
	}

	@summary = "Go to the pageMenuItem of a Site Page. Parent pages hierarchy is required to navigate to child pages."
	macro gotoPageMenuItem {
		if (isSet(parentPagesHierarchy)) {
			for (var parentPage : list "${parentPagesHierarchy}") {
				Click(locator1 = "SitePages#LIST_GROUP_ITEM", key_itemName = "${parentPage}");
			}
		}

		Click(
			locator1 = "SitePages#LIST_GROUP_ITEM_ELLIPSIS_ICON",
			key_pageName = "${pageName}"
		);

		MenuItem.click(menuItem = "${menuItem}");
	}

	macro openPagesAdmin {
		Navigator.openWithAppendToBaseURL(
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_admin_web_portlet_GroupPagesPortlet"
		);
	}

	macro togglePrivacy {
		Click(
			locator1 = "SitePages#LIST_GROUP_ITEM_CARET_ICON_SPECIFIC",
			key_pageName = "${privacy}"
		);
	}
}